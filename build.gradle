plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
	id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
	id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

mixin {
	add sourceSets.main, "twigs.refmap.json"
	config "twigs.mixins.json"
}

version = "${project.minecraft_version}-${project.mod_version}"
group = 'com.ninni.twigs'
archivesBaseName = 'Twigs'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
	mappings channel: 'official', version: "${project.mappings_version}"

	copyIdeResources = true //Calls processResources when in dev
	reobf = false // Forge 1.20.6+ uses official mappings at runtime, so we shouldn't reobf from official to SRG

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'
			arg "-mixin.config=twigs.mixins.json"
			mods {
				twigs {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'
			arg "-mixin.config=twigs.mixins.json"

			mods {
				twigs {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"

			args '--mod', 'twigs', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				twigs {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven {
		url "https://www.cursemaven.com"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
	// this fixes the problem of forge not booting, i dont know why this is needed, thanks forge :) (I learnt why this is need, its a hacky fix for dependency hell)
	implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }

	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : "Twigs",
				"Specification-Vendor"    : "Ninni",
				"Specification-Version"   : "1",
				"Implementation-Title"    : "Twigs",
				"Implementation-Version"  : "1.0.0",
				"Implementation-Vendor"   : "Ninni",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				"MixinConfigs"            : "twigs.mixins.json"
		])
	}
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			url "https://maven.minecraftforge.net"
		}
	}
}

// if this is not here "resources" will not be included
sourceSets.each {
	def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
	it.output.resourcesDir = dir
	it.java.destinationDirectory = dir
}
